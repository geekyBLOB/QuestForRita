// ****************************************************************************
//
// Logic 6: Landing
//
// ****************************************************************************

#include "defines.txt"

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 13) {
     v6 = 1;
     position(ego,59,162);
 }

 if (prev_room_no == 16) {
     v6 = 5;
     position(ego,57,116);
 }

 if (prev_room_no == 17) {
     v6 = 3;
     position(ego,34,128);
 }

  draw(ego);
  show.pic();

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

get.posn(ego, v30, v31);

if (said("look")) {
   print("You are in the hallway.");
}

if (said("open", "door")) {
  reset(if_handled);
  if ((v30 < 36) && (v30 > 23) && (v31 > 119) && (v31 < 134))
  {
      if (isset(tobias_door_open))
      {
          new.room(17);
          set(if_handled);
      }
      else {
          print("Cannot open, door is locked.");
          set(if_handled);
      }
  }

  if (!isset(if_handled))
  {
    print("What door?");
  }
}

if (v31 < 113)
{
  new.room(16);
}

if (v31 > 166)
{
   new.room(13);
}

return();










