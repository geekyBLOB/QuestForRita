// ****************************************************************************
//
// Logic 6: Tobias's room
//
// ****************************************************************************

#include "defines.txt"
#define tobias  o3

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 15) {
     v6 = 3;
     position(ego,9,161);
 }

  draw(ego);
  show.pic();

    animate.obj(tobias);
    load.view(19);
    set.view(tobias,19);
    set.loop(tobias,0); 
    set.cel(tobias,0); 
    position(tobias,121,124); 
    set.priority(tobias,11);
    ignore.objs(tobias);
    draw(tobias);
    reset(tobias_reached_target);
    move.obj(tobias, 66, 124, 1, tobias_reached_target); 

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

if (isset(tobias_reached_target)) {
  if(posn(tobias, 121, 124, 121, 124)) {
     reset(tobias_reached_target);
     move.obj(tobias, 66, 124, 1, tobias_reached_target); 
  }
  else {
     if(posn(tobias, 66, 124, 66, 124)) {
     reset(tobias_reached_target);
     move.obj(tobias, 121, 124, 1, tobias_reached_target); 
  }
  }
}

get.posn(ego, v30, v31);

if (said("look")) {
   print("You are in Tobias's room.");
}

if (said("open", "door")) {
  reset(if_handled);
  if ((v30 < 12) && (v31 > 148) && (v31 < 167))
  {
          new.room(15);
          set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What door?");
  }
}

if (said("give", "twelve", "bird", "roast"))  {
   if (has("twelve bird roast"))  {
       print("Thanks!");
       get("sunglasses");
       drop("twelve bird roast");
       print("You got some sunglasses.");
   }
   else {
      print("You do not have a twelve bird roast to give.");
   }
}

if (said("speak","tobias")) {
   if (!has("sunglasses")) {
        if (!isset(tobias_wants_dinner)) {
           print("Faye: Tobias, can I borrow your sunglasses. I need them to protect my eyeballs.");
           print("Tobias: Yea sure, but only if you make me dinner first.");
           set(tobias_wants_dinner);
        }
        else {
           print("You can have the sunglasses if you cook me dinner.");
        }
   }
   else
   {
       print("What can I do you for?");
   }
}

return();












