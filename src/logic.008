// ****************************************************************************
//
// Logic 6: Johannes's room
//
// ****************************************************************************

#include "defines.txt"

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 6) {
     v6 = 7;
     position(ego,150,158);
 }

  draw(ego);
  show.pic();

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }
  else {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       position(johannes,79,133); 
       set.priority(johannes,12);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       move.obj(johannes, 32, 133, 1, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}
else {
  if (posn(johannes, 32, 133, 32, 133)) {
       reset(johannes_reached_target);
       move.obj(johannes, 79, 133, 1, johannes_reached_target);
  }
  else {
      if (posn(johannes, 79, 133, 79, 133)) {
         reset(johannes_reached_target);
         move.obj(johannes, 32, 133, 1, johannes_reached_target);
      }
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

get.posn(ego, v30, v31);

if (said("look")) {
     print("You are in Johannes's room. He is busy reading a cookbook.");
}

if (said("open", "door")) {
  reset(if_handled);
  if ((v30 > 147) && (v31 > 156) && (v31 < 167))
  {
    new.room(6);
    set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What door?");
  }
}

if (said("speak", "johannes")) {
    if (!isset(johannes_wants_ingrediants)) {
       print("Faye: Tobias want's me to cook dinner for him, but I don't know what to cook.");
       print("Johannes: How about something nice and simple, like a twelve bird roast.");
       print("Faye: Ok, but I don't know how.");
       print("Johannes: I can help you. As it happens I have 10 of the requisite 12 birds, all I need is a chicken, a quail, and an egg");
       set(johannes_wants_ingrediants);
   }
   else
   {
       if(isset(johannes_got_chicken) && isset(johannes_got_quail) && isset(johannes_got_egg)) {
          print("What can I do you for?");
       }
       else {
          print("Johannes: We still need:");
          if (!isset(johannes_got_chicken)) {
             print("A chicken");
          }
          if (!isset(johannes_got_quail)) {
             print("A quail");
          }
          if (!isset(johannes_got_egg)) {
             print("An egg");
          }
       }
   }
}

if (isset(johannes_wants_ingrediants)) {
   if (said("give", "quail")) {
      if (has("quail")) {
         drop("quail");
         set(johannes_got_quail);
         if (isset(johannes_got_chicken) && isset(johannes_got_quail) && isset(johannes_got_egg)) {
             print("Thats all the things. To the kitchen!");
             set(johannes_going_to_cook);
         }
         else {
             print("Johannes: We still need:");
             if (!isset(johannes_got_chicken)) {
                print("A chicken");
             }
             if (!isset(johannes_got_quail)) {
                print("A quail");
             }
             if (!isset(johannes_got_egg)) {
                print("An egg");
             }
         }
      }
      else {
          print("You do not have a quail to give");
      }
   }

   if (said("give", "chicken")) {
      if (has("chicken")) {
         drop("chicken");
         set(johannes_got_chicken);
         if (isset(johannes_got_chicken) && isset(johannes_got_quail) && isset(johannes_got_egg)) {
             print("Thats all the things. To the kitchen!");
             set(johannes_going_to_cook);
         }
         else {
             print("Johannes: We still need:");
             if (!isset(johannes_got_chicken)) {
                print("A chicken");
             }
             if (!isset(johannes_got_quail)) {
                print("A quail");
             }
             if (!isset(johannes_got_egg)) {
                print("An egg");
             }
         }
      }
      else {
          print("You do not have a chicken to give");
      }
   }

   if (said("give", "egg")) {
      if (has("egg")) {
         drop("egg");
         set(johannes_got_egg);
         if (isset(johannes_got_chicken) && isset(johannes_got_quail) && isset(johannes_got_egg)) {
             print("Thats all the things. To the kitchen!");
             set(johannes_going_to_cook);
         }
         else {
             print("Johannes: We still need:");
             if (!isset(johannes_got_chicken)) {
                print("A chicken");
             }
             if (!isset(johannes_got_quail)) {
                print("A quail");
             }
             if (!isset(johannes_got_egg)) {
                print("An egg");
             }
         }
      }
      else {
          print("You do not have an egg to give");
      }
   }
}

return();


















