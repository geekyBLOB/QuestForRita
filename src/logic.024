// ****************************************************************************
//
// Logic 24: Rita flying away
//
// ****************************************************************************

#include "defines.txt"

#define rita  o1

if (new_room) {
    prevent.input();
    status.line.off();

    load.pic(room_no);
    draw.pic(room_no);
    discard.pic(room_no);
    set.horizon(37);

    animate.obj(rita);
    load.view(22);
    set.view(rita,22);
    set.loop(rita,0); 
    set.cel(rita,0); 
    position(rita,0,135); 
    set.priority(rita,14);
    ignore.objs(rita);
    draw(rita);
    
    move.obj(rita, 133, 60, 2, jeff_reached_target);
 
    show.pic();

  small_counter = 0;
  big_counter = 0;
}

if (equaln(small_counter, 158)) {
   small_counter = 0;
   if (equaln(big_counter, 164)) {
       big_counter = 0;
   }
   else {
       addn(big_counter,1);
   }
}
else {
   addn(small_counter, 1);
}

if (isset(jeff_reached_target))
{
    new.room(2);
}

if (equaln(big_counter,0) && equaln(small_counter, 20)) {
  display(0,0,"After being connected to mains for      ");
  display(1,0,"several seconds Rita flies away in shock");
}

return();







