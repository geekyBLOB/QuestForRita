// ****************************************************************************
//
// Logic 4: Kitchin
//
// ****************************************************************************

#include "defines.txt"

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 3) {
     v6 = 7;
     position(ego,140,150);
 }

 if (prev_room_no == 5) {
     v6 = 1;
     position(ego,91,159);
 }

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       position(johannes,90,165); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       move.obj(johannes, 44, 115, 1, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }

  draw(ego);
  show.pic();
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

if (said("look")) {
   print("You are in the kitchin...where food is made.");
}

if (said("use", "kettle"))  {
   if (isset(kettle_flag)) {
      print("You made a cup of tea!");
      get("tea");
   }
   else
   {
       print("There is no need to boil the kettle, no one wants tea.");
    }
}

if (said("open", "microwave")) {
   if (isset(keys_available))
   {
        if (isset(got_keys)) {
           print("There is nothing inside the microwave.");
        }
        else {
           print("You found your keys");
           get("keys");
           set(got_keys);
        }
   }
   else {
       print("There is nothing in the microwave.");
   }
}

if (said("open", "door")) {
  reset(if_handled);
  if ((v30 > 127) && (v31 > 138) && (v31 < 160))
  {
     new.room(3);
     set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What door?");
  }
}

if (isset(johannes_reached_target) && isset(johannes_going_to_cook)) {
   stop.motion(johannes);
   stop.cycling(johannes);
   print("You got a twelve bird roast"); 
   get("twelve bird roast");
   reset(johannes_going_to_cook);
}

if (ego_edge_code == bottom_edge) {   // ego touching bottom edge of screen
  new.room(5);
}

return();



























