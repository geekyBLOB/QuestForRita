// ****************************************************************************
//
// Logic 3: Trago lounge
//
// ****************************************************************************

#include "defines.txt"

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 10) {
     v6 = 1;
     position(ego,133,162);
 }

  draw(ego);
  show.pic();

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

get.posn(ego, v30, v31);

if (said("look")) {
   print("You are in Trago Lounge.");
}

if (said("speak", "barman")) {
   if (isset(crazy_lady_needs_pin) && (!has("chicken") || !isset(johannes_got_chicken))) {
      print("Faye: Excuse me bar keep, was the Portswood crazy singing lady in here last night.");
      print("Barman: Yea, she was sitting at that table over there by the noticeboard.");
      print("Faye: Thanks.");
   }
   else {
      print("Hello, what can I do you for?");
   }
}

if (said("look", "table") && isset(crazy_lady_needs_pin)) {
  reset(if_handled);
  if ((v30 > 103) && (v31 > 120) && (v31 < 160))
  {
       print("There is a small piece of paper with something written on it stuck behind the noticeboard at the bottom. Unfortunatly this world is too low-res to read it.");
       set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What table?");
  }
}


if (said("look", "noticeboard") && isset(crazy_lady_needs_pin)) {
  reset(if_handled);
  if ((v30 > 103) && (v31 > 120) && (v31 < 160))
  {
       print("There is a small piece of paper with something written on it stuck to the back of the noticeboard. Unfortunatly this world is too low-res to read it.");
       set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What noticeboard?");
  }
}

if (ego_edge_code == bottom_edge) {
  new.room(10);
}

return();











