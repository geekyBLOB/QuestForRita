// ****************************************************************************
//
// Logic 6: Shed
//
// ****************************************************************************

#include "defines.txt"
#define mechanical_arm     o1
#define falling_box     o2
#define spondulix   o3
#define sparks o4

if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);

 if (prev_room_no == 2) {
     v6 = 1;
     position(ego,79,161);
 }

  draw(ego);

  animate.obj(mechanical_arm);
  load.view(1);
  set.view(mechanical_arm,1);
  set.loop(mechanical_arm,0); 
  set.cel(mechanical_arm,0); 
  position(mechanical_arm,30,90); 
  set.priority(mechanical_arm,11);
  ignore.objs(mechanical_arm);
  draw(mechanical_arm); 

  animate.obj(falling_box);
  load.view(2);
  set.view(falling_box,2);
  set.loop(falling_box,0);
  set.cel(falling_box,0);
  position(falling_box,60,120);
  set.priority(falling_box,11);
  ignore.objs(falling_box);
  draw(falling_box);

  animate.obj(sparks);
  load.view(30);
  set.view(sparks,30);
  set.loop(sparks,0);
  set.cel(sparks,0);
  position(sparks,15,93);
  set.priority(sparks,11);
  ignore.objs(sparks);
  draw(sparks);

  if (!isset(spondulix_picked_up)) {
    animate.obj(spondulix);
    load.view(29);
    set.view(spondulix,29);
    set.loop(spondulix,0); 
    set.cel(spondulix,0); 
    position(spondulix,87,136); 
    set.priority(spondulix,14);
    ignore.objs(spondulix);
    draw(spondulix); 
  }

  show.pic();

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }


}

get.posn(ego, v30, v31);

if (isset(spondulix_picked_up)) {
   if (said("look")) {
      print("You are in the shed. The lathe is generating a million things.");
   }
}
else {
   if (said("pickup", "spondulix")) {
      if (isset(spondulix_picked_up)) {
         print("There is no spondulix to pick up.");
      }
      else {
         get("spondulix");
         set(spondulix_picked_up);
         erase(spondulix);
         print("You picked up a spondulix!");
      }
   }

   if (said("look")) {
      print("You are in the shed. The lathe has generated something, and it is lying on the floor.");
   }
}


if (ego_edge_code == bottom_edge) {   // ego touching bottom edge of screen
  new.room(2);
}

return();









