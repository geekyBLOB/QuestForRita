// ****************************************************************************
//
// Logic 6: Jeff's room
//
// ****************************************************************************

#include "defines.txt"
#define blowtorch o1
if (new_room) {
  load.pic(room_no);
  draw.pic(room_no);
  discard.pic(room_no);
  set.horizon(37);
  reset(jeff_reached_target);

 if (prev_room_no == 6) {
     v6 = 7;
     position(ego,150,162);
 }

  draw(ego);
  show.pic();

  if (!isset(jeff_free)) {
      animate.obj(jeff);
       load.view(18);
       set.view(jeff,18);
       set.loop(jeff,0); 
       set.cel(jeff,0); 
       position(jeff,80,150); 
       set.priority(jeff,14);
       draw(jeff);
  }
  else {
      reset(jeff_reached_target);
      animate.obj(jeff);
       load.view(3);
       set.view(jeff,3);
       set.loop(jeff,0); 
       set.cel(jeff,0); 
       position(jeff,50,163); 
       set.priority(jeff,14);
       draw(jeff);
       move.obj(jeff, 11, 163, 1, jeff_reached_target);
  }

  if (!has("blowtorch")) {
      animate.obj(blowtorch);
       load.view(25);
       set.view(blowtorch,25);
       set.loop(blowtorch,0); 
       set.cel(blowtorch,0); 
       position(blowtorch,19,124); 
       set.priority(blowtorch,14);
       draw(blowtorch);
  }

  if (isset(johannes_going_to_cook))
  {
       animate.obj(johannes);
       load.view(5);
       set.view(johannes,5);
       set.loop(johannes,0); 
       set.cel(johannes,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(johannes,v30,v31); 
       set.priority(johannes,11);
       ignore.objs(johannes);
       draw(johannes);
       reset(johannes_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
  }

  if (isset(lou_accepted_invite))
  {
       animate.obj(lou);
       load.view(8);
       set.view(lou,8);
       set.loop(lou,0); 
       set.cel(lou,0); 
       get.posn(ego, v30, v31);
       addn(v30,5);
       position.v(lou,v30,v31); 
       set.priority(lou,11);
       ignore.objs(lou);
       draw(lou);
       reset(lou_reached_target);
       get.posn(ego, v30, v31);
       move.obj.v(lou, v30, v31, 2, lou_reached_target);
  }
}

if (isset(johannes_going_to_cook)) {
   get.posn(johannes, johannes_x, johannes_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, johannes_x)) {
      subv(v30, johannes_x);
      assignv(johannes_distx, v30);
   }
   else {
      subv(johannes_x, v30);
      assignv(johannes_distx, johannes_x);
   }

   if (greaterv(v31, johannes_y)) {
      subv(v31, johannes_y);
      assignv(johannes_disty, v31);
   }
   else {
      subv(johannes_y, v31);
      assignv(johannes_disty, johannes_y);
  }

   if (lessn(johannes_distx, 5) && lessn(johannes_disty, 5)) {
      stop.cycling(johannes);
      stop.motion(johannes);
   }

   if ((!lessn(johannes_distx, 7) || !lessn(johannes_disty, 7))) {
      start.cycling(johannes);
      get.posn(ego, v30, v31);
      move.obj.v(johannes, v30, v31, 2, johannes_reached_target);
   }
}

if (isset(lou_accepted_invite)) {
   get.posn(lou, lou_x, lou_y);
   get.posn(ego, v30, v31);

   if (greaterv(v30, lou_x)) {
      subv(v30, lou_x);
      assignv(lou_distx, v30);
   }
   else {
      subv(lou_x, v30);
      assignv(lou_distx, lou_x);
   }

   if (greaterv(v31, lou_y)) {
      subv(v31, lou_y);
      assignv(lou_disty, v31);
   }
   else {
      subv(lou_y, v31);
      assignv(lou_disty, lou_y);
  }

   if (lessn(lou_distx, 5) && lessn(lou_disty, 5)) {
      stop.cycling(lou);
      stop.motion(lou);
   }

   if ((!lessn(lou_distx, 7) || !lessn(lou_disty, 7))) {
      start.cycling(lou);
      get.posn(ego, v30, v31);
      move.obj.v(lou, v30, v31, 2, lou_reached_target);
   }
}

get.posn(ego, v30, v31);

if (said("look")) {
   if (isset(jeff_free)) {
       print("You are in Jeff's room");
   }
   else {
        print("You are in Jeff's room, in his haste he has become lodged beneith a lathe.");
   }
}

if (said("speak", "jeff")){
   if (isset(jeff_free)) {
      print("Hello, what can I do you for?");
   }
   else {
      print("Help! In my haste I have become stuck beneith one of my lathes!");
   }
}

if (said("use", "wheelbarrow")) {
   if (has("wheelbarrow")) {
       print("Thanks!");
       stop.motion(ego);
      erase(jeff);
      animate.obj(jeff);
       load.view(3);
       set.view(jeff,3);
       set.loop(jeff,0); 
       set.cel(jeff,0); 
       position(jeff,80,150); 
       set.priority(jeff,14);
       draw(jeff);
       ignore.objs(jeff);
       reset(jeff_reached_target);
       drop("wheelbarrow");
       move.obj(jeff, 70, 164, 1, jeff_reached_target);
     }
      else
      {
           print("You do not have a wheelbarrow.");
       }
}

if (isset(jeff_reached_target)) {
  if (!isset(jeff_free)) {
    if (posn(jeff, 69,163, 71, 165)) {
         reset(jeff_reached_target);
         move.obj(jeff, 20, 164, 1, jeff_reached_target);
    }
    else {
         if (posn(jeff, 20,164, 20, 164)) {
            reset(jeff_reached_target);
            erase(blowtorch);
            move.obj(jeff, 90, 161, 1, jeff_reached_target);
         }
         else {
            if (posn(jeff, 90,161, 90, 161)) {
               reset(jeff_reached_target);
               subn(v30,10);
               move.obj.v(jeff, v30, v31, 2, jeff_reached_target);
            }
            else {
               print("Jeff gave you a blowtorch.");
               get("blowtorch");
               reset(jeff_reached_target);
               set(jeff_free);
               stop.cycling(jeff);
               start.motion(ego);
            }
        }
    }
  }
  else {
     if (posn(jeff, 50,163, 50, 163)) {
         move.obj(jeff, 11, 163, 1, jeff_reached_target);
     }
     else {
        move.obj(jeff, 50, 163, 1, jeff_reached_target);
     }
  }
}

if (said("open", "door")) {
  reset(if_handled);
  if ((v30 > 147) && (v31 > 156) && (v31 < 167))
  {
    new.room(6);
    set(if_handled);
  }

  if (!isset(if_handled))
  {
    print("What door?");
  }
}

return();





















